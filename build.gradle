plugins {
    id 'java'
    id 'groovy'
}

def componentNode = parseComponent(project)
def moquiDir = projectDir.parentFile.parentFile.parentFile
def frameworkDir = file(moquiDir.absolutePath + '/framework')
def libDir = projectDir.absolutePath + '/lib'

group 'module.seeds.template'
version = componentNode.'@version'
description 'Template Module'

// dependency repositories
repositories {
    flatDir name: 'frameworkLib', dirs: frameworkDir.absolutePath + '/lib'
    flatDir name: 'projectLib', dirs: projectDir.absolutePath + '/lib'
    flatDir name: 'repoLib', dirs: projectDir.absolutePath + '/librepo'
    flatDir name: 'fopLib', dirs: moquiDir.absolutePath + '/runtime/component/module-seeds-core/librepo'
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation (
            project(':framework')
    )
    testImplementation project(':framework').configurations.testImplementation.allDependencies
}

// log4j has annotation processors, disable to avoid warning
tasks.withType(JavaCompile) { options.compilerArgs << "-proc:none" }
tasks.withType(GroovyCompile) { options.compilerArgs << "-proc:none" }

// by default the Java plugin runs test on build, change to not do that (only run test if explicit task)
// no longer works as of gradle 4.8 or possibly earlier, use clear() instead: check.dependsOn.remove(test)
check.dependsOn.clear()

task cleanLib(type: Delete) {
    delete fileTree(dir: libDir, include: '*')
}
clean.dependsOn cleanLib

task copyDependencies {
    doLast {
        copy {
            from (configurations.runtimeClasspath - project(':framework').configurations.runtimeClasspath - project(':framework').jar.archivePath)
            into file(projectDir.absolutePath + '/lib')
        }
    }
}
copyDependencies.dependsOn cleanLib

jar {
    destinationDir = file(libDir)
    baseName = componentNode.'@name'
}
jar.dependsOn copyDependencies
